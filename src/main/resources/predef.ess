public let apply = { f x -> (f x) }
public let ident = { x -> x }
public let >> = { f g x -> (g (f x)) }
public let pipe = { x fs -> (List.fold (flip apply) x fs) }
public let flip = { f a b -> (f b a) }

public let if = { (Bool cond) (Function then) (Function else) ->
  (match cond [
    { (True) -> (then) }
    { (False) -> (else) }
  ])
}

public type Option = Some x | None

public type Tuple = T2 a b | T3 a b c | T4 a b c d

public namespace List {
  public let fold = { (Function folder) state (List list) ->
    (match list [
      { [] -> state }
      { (:: h t) -> (. folder (folder state h) t) }
    ])
  }

  public let reverse = (fold (flip ::) [])

  public let foldr = { (Function folder) state (List list) -> (fold folder state (reverse list)) }

  public let map = { (Function mapper) (List list) ->
    (foldr { acc x -> (:: (mapper x) acc) } [] list)
  }

  public let foreach = { (Function f) (List list) ->
    (match list [
      { [] -> () }
      { (:: h t) ->
        let _ = (f h)
        (. f t)
      }
    ])
  }

  public let unfold = { (Function unfolder) init ->
    (reverse
      ({ state acc ->
        (match (unfolder state) [
          { (None) -> acc }
          { (Some (T2 a b)) -> (. b (:: a acc)) }
        ])
      } init [])
    )
  }

  public let range = { (Number n) ->
    (unfold { i ->
      (if (eq i n)
        { -> None }
        { -> (Some (T2 i (+ i 1))) }
      )
    } 0)
  }
}

public namespace String {
  public let join = { (String sep) (List strs) ->
    (match strs [
      { [] -> "" }
      { (:: h t) ->
        (List.fold
          { s e -> (String.concat (String.concat s sep) e) }
          h
          t
        )
      }
    ])
  }
}
